# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0

name                openafs-client
version             1.8.6
revision            4
categories          sysutils
license             IPL-1
maintainers         mit.edu:sipb-macathena
description         AFS distributed fileystem client
long_description    AFS is a distributed filesystem allowing cross-platform \
                    sharing of files among multiple computers.  Facilities \
                    are provided for access control, authentication, backup \
                    and administrative management.
homepage            http://www.openafs.org/
master_sites        http://openafs.org/dl/openafs/${version}/
distname            openafs-${version}-src

checksums           openafs-1.8.6-src.tar.gz \
                    rmd160  4a59624b935f25c9819902dfd38ee54d66b2a8d6 \
                    sha256  9efc374ef32b90ed62e59280535babff684d1bb551889d08abbf3e9d93e2e7d2 \
                    size    17789990 \
                    OpenAFS-1.8.5-osxsna3-Mojave.dmg \
                    rmd160  ed9fc4630feb30505d471474b1d5f5c000b52a82 \
                    sha256  1c8d28bba89d846f72576999f0d78990f8f09d4a3cc599e7182b9801ed1c2542 \
                    size    16959208 \
                    OpenAFS-1.8.5-osxsna3-Catalina.dmg \
                    rmd160  8e29c61062edae2172ebc8cf5249fed07e9c7f2a \
                    sha256  5bb07d7905e17a65dec8897710c4146ad754b7c706d681e2d0e1883c63b8c206 \
                    size    17514101 \
                    CellServDB \
                    rmd160  b63cce0e17b6a21aebc15165de377ab560eacce5 \
                    sha256  43c5f0c0283eb2a1e03ea4b4d680922037a3fa495a024927acefd2f029c5d917 \
                    size    35258

universal_variant   no

depends_run         port:perl5

if {${name} eq ${subport}} {
    worksrcdir openafs-${version}

    # Allow xcodebuild for the prefpane
    use_xcode           yes

    depends_build       port:swig-perl

    patchfiles \
        swig-com_err.patch \
        afsd_fuse.patch \
        xcodebuild.patch
    patch.args          -p1

    post-patch {
        reinplace "s|/var/db/openafs/etc|${prefix}/etc/openafs|" src/platform/DARWIN/afssettings.m
    }

    if {${os.major} >= 13} {
        configure.args-append --disable-kernel-module
        depends_run-append port:openafs-signed-kext
    }

    post-destroot {
        # Create default config
        file mkdir ${destroot}${prefix}/etc/openafs
        system "echo athena.mit.edu > ${destroot}${prefix}/etc/openafs/ThisCell"
        file mkdir ${destroot}${prefix}/etc/openafs/config
        #ln -s ${prefix}/etc/openafs/ThisCell ${destroot}${prefix}/etc/openafs/config/ThisCell
        system "echo athena > ${destroot}${prefix}/etc/openafs/CellAlias"
        #ln -s ${prefix}/etc/openafs/CellAlias ${destroot}${prefix}/etc/openafs/config/CellAlias
    }

    destroot.target install dest

    master_sites-append http://grand.central.org/dl/cellservdb/:csdb
    distfiles-append CellServDB:csdb
    post-destroot {
        copy ${distpath}/CellServDB ${destroot}${prefix}/etc/openafs/CellServDB
    }
}

subport openafs-signed-kext {
    description Signed Kernel Extension for AFS on 10.9+
    # 1.8.6 doesn't have a kext yet
    version 1.8.5
    long_description ${description}
    set mp.dist {
        13 9 Mavericks
        14 10 Yosemite
        15 11 ElCapitan
        16 12 Sierra
        17 13 HighSierra
        18 14 Mojave
        19 15 Catalina
    }
    foreach { mp.darwin mp.os mp.name } ${mp.dist} {
        if {${os.major} == ${mp.darwin}} {
            # https://download.sinenomine.net/openafs/bins/1.8.5/macos-10.15/OpenAFS-1.8.5-osxsna3-Catalina.dmg
            master_sites https://download.sinenomine.net/openafs/bins/${version}/macos-10.${mp.os}
            distfiles OpenAFS-${version}-osxsna3-${mp.name}.dmg
            extract.only OpenAFS-${version}-osxsna3-${mp.name}.dmg
            use_dmg yes
            post-extract {
                move ${workpath}/${distname} ${workpath}/openafs-dmg
                system -W ${workpath}/openafs-dmg "pkgutil --expand 'OpenAFS.pkg' ${workpath}/openafs-pkg"
                file mkdir ${workpath}/openafs-unpacked
                system -W ${workpath}/openafs-unpacked "gzip -dc ../openafs-pkg/OpenAFS-dist.pkg/Payload | cpio -id"
            }
            post-patch {
                # OpenAFS libraries have hard-coded paths that must be removed.
                set toolsdir ${workpath}/openafs-unpacked/Library/OpenAFS/Tools
                set libs [glob -directory ${toolsdir} -tails lib/*.dylib]
                set args "-add_rpath ${prefix}/Library/OpenAFS"
                foreach lib $libs {
                    append args " -change /Library/OpenAFS/Tools/${lib} @rpath/${lib}"
                }
                foreach lib $libs {
                    if {[file type "${toolsdir}/${lib}"] ne "link"} {
                        system "install_name_tool $args ${toolsdir}/${lib}"
                        system "codesign --remove-signature ${toolsdir}/${lib}"
                    }
                }
                system "install_name_tool $args ${workpath}/openafs-unpacked/Library/OpenAFS/Tools/root.client/usr/vice/etc/afsd"
                system "codesign --remove-signature ${workpath}/openafs-unpacked/Library/OpenAFS/Tools/root.client/usr/vice/etc/afsd"
            }
            configure {}
            build {}
            destroot {
                set pkgpath ${workpath}/openafs-unpacked
                file mkdir ${destroot}${prefix}/Library/OpenAFS
                copy ${pkgpath}/Library/OpenAFS/Tools/root.client/usr/vice/etc \
                    ${pkgpath}/Library/OpenAFS/Tools/lib \
                    ${destroot}${prefix}/Library/OpenAFS
                system "dot_clean ${destroot}${prefix}/Library/OpenAFS"

                # Install startup/shutdown script
                copy -force ${filespath}/afs.rc ${destroot}${prefix}/Library/OpenAFS/etc/afs.rc
                reinplace "s|@PREFIX@|${prefix}|g" ${destroot}${prefix}/Library/OpenAFS/etc/afs.rc

                # TODO: Install this config as part of openafs-client?
                # Create default config
                file mkdir ${destroot}${prefix}/etc/openafs
                copy ${pkgpath}/private/var/db/openafs/etc/config/settings.plist.orig ${destroot}${prefix}/etc/openafs/settings.plist
                file mkdir ${destroot}${prefix}/var/db/openafs/cache
                destroot.keepdirs-append ${destroot}${prefix}/var/db/openafs/cache
                system "echo /afs:${prefix}/var/db/openafs/cache:100000 > ${destroot}${prefix}/etc/openafs/cacheinfo"
            }
            startupitem.create yes
            startupitem.autostart yes
            startupitem.start "${prefix}/Library/OpenAFS/etc/afs.rc start"
            startupitem.stop "${prefix}/Library/OpenAFS/etc/afs.rc start"
            startupitem.logfile "${prefix}/var/log/openafs.log"
            post-activate {
                if {[file exists /usr/share/firmlinks]} {
                    if {[catch {system "grep ^afs /etc/synthetic.conf"}]} {
                        system "echo afs >> /etc/synthetic.conf"
                        notes-append {
                            You must restart your computer to create /afs.
                        }
                    }
                } else {
                    if {! [file exists /afs]} {
                        file mkdir /afs
                    }
                }
                system "/usr/libexec/PlistBuddy -c \"Print :SkipPaths:\" /Library/Preferences/com.apple.TimeMachine.plist | grep /afs || /usr/libexec/PlistBuddy -c 'Add :SkipPaths: string \"/afs\"' /Library/Preferences/com.apple.TimeMachine.plist"
                if {[catch {system "kextload ${prefix}/Library/OpenAFS/etc/afs.kext"}]} {
                    notes-append {
                        You must allow the kernel extension to load by visiting the Security
                        preference pane. You can go there by running:
                        open "x-apple.systempreferences:com.apple.preference.security?General"
                    }
                }
            }
        }
    }
}
